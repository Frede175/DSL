/*
 * generated by Xtext 2.24.0
 */
package dk.sdu.mmmi.validation

import org.eclipse.xtext.validation.Check
import dk.sdu.mmmi.typescriptdsl.TypescriptdslPackage.Literals
import dk.sdu.mmmi.typescriptdsl.Extendable
import dk.sdu.mmmi.typescriptdsl.ModuleRefernce

/**
 * This class contains custom validation rules. 
 *
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#validation
 */
class TypescriptdslValidator extends AbstractTypescriptdslValidator {
	
	
	@Check
	def void validateNoCycleModuleReference(Extendable extendable) {
		if (extendable.modules.length == 0) return
		
		val visited = newHashSet(extendable)
		
		val current = newLinkedList()
		current.addAll(extendable.modules.map[it.module])
		while (!current.empty) {
			val m = current.pop
			if (visited.contains(m)) {
				error("Cycle in hierarchy of modules '"+ m.name + "'", Literals.EXTENDABLE__NAME)
				return
			}
			current.addAll(m.modules.map[it.module])
			visited.add(m)
		}
	}
	
	@Check
	def void validateRequiredParametersForGenerics(ModuleRefernce ref) {
		if (ref.type === null && ref.module.generic !== null) {
			error('''Module «ref.module.name» requires a generic parameter''', Literals.MODULE_REFERNCE__TYPE)
		}
	}
}
